# Copyright 2021 Nikita Melekhin. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Common
  debug_build = true
  optimize = true
  host = "gnu"
  llvm_bin_path = ""
  device_type = "desktop"
  target_board = ""

  # Kernel Flags
  kernel_symbols = true
  kernel_preempt = true

  # Userland
  userland_symbols = true
  compile_tests = true
  objc_support = false
  test_method = "none"
}

if (target_cpu == "") {
  target_cpu = "x86"
}
if (target_cpu == "arm") {
  target_cpu = "arm32"
}
if (device_type == "m") {
  device_type = "mobile"
}
if (device_type == "d") {
  device_type = "desktop"
}
if (target_cpu == "arm32") {
  # Currently we support only vexpress-a15.
  target_board = "vexpress-a15"
}
if (target_cpu == "x86") {
  target_board = "i686"
}
if (target_cpu == "aarch64") {
  if (target_board == "") {
    target_board = "qemu_virt"
  }
}

if (host == "gnu") {
  set_default_toolchain("//toolchains:gnu-cross-compiler")
}

if (host == "llvm") {
  objc_support = true
  llvm_bin_path = getenv("LLVM_BIN_PATH")
  set_default_toolchain("//toolchains:llvm-cross-compiler")
}
